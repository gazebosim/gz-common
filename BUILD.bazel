load("@buildifier_prebuilt//:rules.bzl", "buildifier", "buildifier_test")
load("@rules_gz//gz:headers.bzl", "gz_configure_header", "gz_export_header", "gz_include_header")
load("@rules_license//rules:license.bzl", "license")

package(
    default_applicable_licenses = [":license"],
)

license(
    name = "license",
    package_name = "gz-common",
)

exports_files([
    "LICENSE",
    "MODULE.bazel",
])

gz_configure_header(
    name = "config",
    src = "include/gz/common/config.hh.in",
    cmakelists = ["CMakeLists.txt"],
    package = "common",
)

gz_export_header(
    name = "include/gz/common/Export.hh",
    export_base = "GZ_COMMON",
    lib_name = "gz-common",
    visibility = ["//visibility:private"],
)

public_headers_no_gen = glob([
    "include/gz/common/*.hh",
    "include/gz/common/detail/*.hh",
])

private_headers = glob(["src/*.hh"])

sources = glob(
    ["src/*.cc"],
    exclude = ["src/*_TEST.cc"],
)

gz_include_header(
    name = "common_hh_genrule",
    out = "include/gz/common.hh",
    hdrs = public_headers_no_gen + [
        "include/gz/common/Export.hh",
        "include/gz/common/config.hh",
    ],
)

public_headers = public_headers_no_gen + [
    "include/gz/common/config.hh",
    "include/gz/common/Export.hh",
    "include/gz/common.hh",
]

cc_library(
    name = "gz-common",
    srcs = sources + private_headers,
    hdrs = public_headers,
    copts = ["-fexceptions"],
    includes = ["include"],
    visibility = ["//visibility:public"],
    deps = [
        "@gz-utils",
        "@libuuid//:uuid",
    ],
)

# Tests
test_sources = glob(
    [
        "src/*_TEST.cc",
    ],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src],
    deps = [
        ":gz-common",
        "//testing",
        "@gz-math",
        "@googletest//:gtest_main",
    ],
) for src in test_sources]

# Bazel linting
buildifier(
    name = "buildifier.fix",
    exclude_patterns = ["./.git/*"],
    lint_mode = "fix",
    mode = "fix",
)

buildifier_test(
    name = "buildifier.test",
    exclude_patterns = ["./.git/*"],
    lint_mode = "warn",
    mode = "diff",
    no_sandbox = True,
    workspace = "//:MODULE.bazel",
)
