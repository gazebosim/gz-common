load(
    "//ign_bazel:build_defs.bzl",
    "IGNITION_FEATURES",
    "IGNITION_ROOT",
    "IGNITION_VISIBILITY",
    "generate_include_header",
    "ign_config_header",
    "ign_export_header",
)

package(
    default_visibility = IGNITION_VISIBILITY,
    features = IGNITION_FEATURES,
)

licenses(["notice"])

exports_files(["LICENSE"])

PROJECT_NAME = "ignition-common"

PROJECT_MAJOR = 5

PROJECT_MINOR = 0

PROJECT_PATCH = 0

# Generates config.hh based on the version numbers in CMake code.
ign_config_header(
    name = "config",
    src = "include/gz/common/config.hh.in",
    cmakelists = ["CMakeLists.txt"],
    project_name = PROJECT_NAME,
    project_version = (PROJECT_MAJOR, PROJECT_MINOR, PROJECT_PATCH),
)

ign_export_header(
    name = "include/gz/common/Export.hh",
    export_base = "IGNITION_COMMON",
    lib_name = "ignition-common",
    visibility = ["//visibility:private"],
)

public_headers_no_gen = glob([
    "include/gz/common/*.hh",
    "include/gz/common/detail/*.hh",
])

private_headers = [
    "src/PluginUtils.hh",
    "src/PrintWindowsSystemWarning.hh",
]

sources = glob(
    ["src/*.cc"],
    exclude = ["src/*_TEST.cc"],
)

generate_include_header(
    name = "commonhh_genrule",
    out = "include/gz/common.hh",
    hdrs = public_headers_no_gen + [
        "include/gz/common/config.hh",
        "include/gz/common/Export.hh",
    ],
)

public_headers = public_headers_no_gen + [
    "include/gz/common/config.hh",
    "include/gz/common/Export.hh",
    "include/gz/common.hh",
]

cc_library(
    name = "ign_common",
    srcs = sources + private_headers,
    hdrs = public_headers,
    includes = ["include"],
    linkopts = ["-ldl"],
    deps = [
        "@uuid",
        IGNITION_ROOT + "ign_math",
    ],
)

cc_binary(
    name = "libignition-common5.so",
    includes = ["include"],
    linkopts = ["-Wl,-soname,libignition-common5.so"],
    linkshared = True,
    deps = [":ign_common"],
)

test_srcs = glob(
    ["src/*_TEST.cc"],
    exclude = ["src/PluginLoader_TEST.cc"],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src],
    data = [":libignition-common5.so"],
    deps = [
        ":ign_common",
        IGNITION_ROOT + "ign_bazel:utilities",
        IGNITION_ROOT + "ign_common/test:test_utils",
        "@gtest",
        "@gtest//:gtest_main",
    ],
) for src in test_srcs]
