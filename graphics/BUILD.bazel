load("@rules_gazebo//gazebo:headers.bzl", "gz_export_header")
load("@rules_license//rules:license.bzl", "license")

package(
    default_applicable_licenses = ["//:license"],
    features = [
        "layering_check",
        "parse_headers",
    ],
)

public_headers_no_gen = glob([
    "include/gz/common/*.hh",
    "include/gz/common/**/*.hh",
])

vhacd_header = ["src/VHACD/VHACD.h"]
tiny_obj_loader_header = ["src/tiny_obj_loader.h"]

sources = glob(
    ["src/*.cc"],
    exclude = ["src/*_TEST.cc"],
)

test_sources = glob(["src/*_TEST.cc"])

gz_export_header(
    name = "Export",
    out = "include/gz/common/graphics/Export.hh",
    export_base = "GZ_COMMON_GRAPHICS",
    lib_name = "gz-common-graphics",
)

public_headers = public_headers_no_gen + [
    "include/gz/common/graphics/Export.hh",
]

cc_library(
    name = "graphics",
    srcs = sources + vhacd_header + tiny_obj_loader_header,
    hdrs = public_headers,
    copts = [
        "-Wno-implicit-fallthrough",
        "-Wno-unused-value",
    ],
    includes = ["include"],
    visibility = ["//visibility:public"],
    deps = [
        "//:gz-common",
        "@assimp",
        "@cdt",
        "@freeimage//:freeimage",
        "@gz-utils//:ImplPtr",
        "@tinyxml2",
    ],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src],
    data = ["//test:data"],
    env = {
        "GZ_BAZEL": "1",
    },
    deps = [
        ":graphics",
        "//testing:testing",
        "@googletest//:gtest",
        "@googletest//:gtest_main",
    ],
) for src in test_sources]
