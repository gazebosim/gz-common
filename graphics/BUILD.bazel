load(
    "//gz_bazel:build_defs.bzl",
    "GZ_ROOT",
    "GZ_VISIBILITY",
    "generate_include_header",
    "gz_export_header",
)

package(
    default_visibility = GZ_VISIBILITY,
    features = [
        "-parse_headers",
        "-layering_check",
    ],
)

public_headers_no_gen = glob([
    "include/gz/common/*.hh",
    "include/gz/common/**/*.hh",
])

sources = glob(
    ["src/*.cc"],
    exclude = ["src/*_TEST.cc"],
)

test_sources = glob(["src/*_TEST.cc"])

gz_export_header(
    name = "include/gz/common/graphics/Export.hh",
    export_base = "GZ_COMMON_GRAPHICS",
    lib_name = "gz-common-graphics",
    visibility = ["//visibility:private"],
)

generate_include_header(
    name = "graphics_hh_genrule",
    out = "include/gz/common/graphics.hh",
    hdrs = public_headers_no_gen + [
        "include/gz/common/graphics/Export.hh",
    ],
)

public_headers = public_headers_no_gen + [
    "include/gz/common/graphics/Export.hh",
    "include/gz/common/graphics.hh",
    "src/tiny_obj_loader.h",
]

cc_library(
    name = "graphics",
    srcs = sources,
    hdrs = public_headers,
    includes = ["include"],
    deps = [
        "@freeimage",
        "@glib",
        "@gts",
        "@tinyxml2",
        GZ_ROOT + "gz_common",
        GZ_ROOT + "gz_utils",
    ],
)

cc_binary(
    name = "libgz-common6-graphics.so",
    includes = ["include"],
    linkopts = ["-Wl,-soname,libgz-common6-graphics.so"],
    linkshared = True,
    deps = [":graphics"],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src],
    data = [
        GZ_ROOT + "gz_common/test:data",
    ],
    deps = [
        ":graphics",
        GZ_ROOT + "gz_common/test:test_utils",
        "@gtest",
        "@gtest//:gtest_main",
    ],
) for src in test_sources]
