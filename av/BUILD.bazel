load(
    "//gz_bazel:build_defs.bzl",
    "GZ_ROOT",
    "GZ_VISIBILITY",
    "generate_include_header",
    "gz_export_header",
)

package(
    default_visibility = GZ_VISIBILITY,
    features = [
        "-parse_headers",
        "-layering_check",
    ],
)

public_headers_no_gen = glob([
    "include/gz/common/*.hh",
    "include/gz/common/**/*.hh",
])

sources = glob(
    ["src/*.cc"],
    exclude = [
        "src/*_TEST.cc",
        "src/HWEncoder.cc",
    ],
)

test_sources = glob(["src/*_TEST.cc"])

gz_export_header(
    name = "include/gz/common/av/Export.hh",
    export_base = "GZ_COMMON_AV",
    lib_name = "gz-common-av",
    visibility = ["//visibility:private"],
)

generate_include_header(
    name = "eventshh_genrule",
    out = "include/gz/common/av.hh",
    hdrs = public_headers_no_gen + [
        "include/gz/common/av/Export.hh",
    ],
)

public_headers = public_headers_no_gen + [
    "include/gz/common/av/Export.hh",
    "include/gz/common/av.hh",
]

cc_library(
    name = "av",
    srcs = sources,
    hdrs = public_headers,
    includes = ["include"],
    deps = [
        GZ_ROOT + "gz_common",
        "@ffmpeg//:avcodec",
        "@ffmpeg//:avdevice",
        "@ffmpeg//:avformat",
        "@ffmpeg//:avutil",
        "@ffmpeg//:swscale",
    ],
)

cc_binary(
    name = "libgz-common6-av.so",
    includes = ["include"],
    linkopts = ["-Wl,-soname,libgz-common6-av.so"],
    linkshared = True,
    deps = [":av"],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src],
    data = [
        GZ_ROOT + "gz_common/test:data",
    ],
    deps = [
        ":av",
        GZ_ROOT + "gz_common/test:test_utils",
        GZ_ROOT + "gz_bazel:utilities",
        "@gtest",
        "@gtest//:gtest_main",
    ],
) for src in test_sources]
