load(
    "//gz_bazel:build_defs.bzl",
    "GZ_ROOT",
    "GZ_VISIBILITY",
    "generate_include_header",
    "gz_export_header",
)

package(
    default_visibility = GZ_VISIBILITY,
    features = [
        "-parse_headers",
        "-layering_check",
    ],
)

public_headers_no_gen = glob([
    "include/gz/common/*.hh",
    "include/gz/common/events/*.hh",
])

sources = glob(
    ["src/*.cc"],
    exclude = ["src/*_TEST.cc"],
)

test_sources = glob(["src/*_TEST.cc"])

gz_export_header(
    name = "include/gz/common/events/Export.hh",
    export_base = "GZ_COMMON_EVENTS",
    lib_name = "gz-common-events",
    visibility = ["//visibility:private"],
)

generate_include_header(
    name = "eventshh_genrule",
    out = "include/gz/common/events.hh",
    hdrs = public_headers_no_gen + [
        "include/gz/common/events/Export.hh",
    ],
)

public_headers = public_headers_no_gen + [
    "include/gz/common/events/Export.hh",
    "include/gz/common/events.hh",
]

cc_library(
    name = "events",
    srcs = sources,
    hdrs = public_headers,
    includes = ["include"],
    deps = [
        GZ_ROOT + "gz_common",
    ],
)

cc_binary(
    name = "libgz-common6-events.so",
    includes = ["include"],
    linkopts = ["-Wl,-soname,libgz-common6-events.so"],
    linkshared = True,
    deps = [":events"],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src],
    deps = [
        ":events",
        GZ_ROOT + "gz_common/test:test_utils",
        "@gtest",
        "@gtest//:gtest_main",
    ],
) for src in test_sources]
