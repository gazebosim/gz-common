load("@buildifier_prebuilt//:rules.bzl", "buildifier", "buildifier_test")
load("@rules_gazebo//gazebo:headers.bzl", "gz_export_header")
load("@rules_license//rules:license.bzl", "license")

package(
    default_applicable_licenses = [":license"],
    features = ["layering_check"],
)

license(
    name = "license",
    package_name = "gz-common-events",
)

public_headers = glob(
    include = [
        "include/gz/common/*.hh",
        "include/gz/common/**/*.hh",
    ],
) + [
        "include/gz/common/events/Export.hh",
]

sources = glob(
    include = ["src/*.cc"],
    exclude = ["src/*_TEST.cc"],
)

test_sources = glob(
    include = ["src/*_TEST.cc"],
)

gz_export_header(
    name = "Export",
    out = "include/gz/common/events/Export.hh",
    export_base = "GZ_COMMON_EVENTS",
    lib_name = "gz-common-events",
)

cc_library(
    name = "gz-common-events",
    srcs = sources,
    hdrs = public_headers,
    includes = ["include"],
    visibility = ["//visibility:public"],
    deps = [
        "//:gz-common",
        "@gz-utils//:ImplPtr",
    ],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src],
    deps = [
        ":gz-common-events",
        "//testing:gz-common-testing",
        "@googletest//:gtest_main",
    ],
) for src in test_sources]
