load(
    "//gz_bazel:build_defs.bzl",
    "GZ_ROOT",
    "GZ_VISIBILITY",
    "generate_include_header",
    "gz_export_header",
)

package(
    default_visibility = GZ_VISIBILITY,
    features = [
        "-parse_headers",
        "-layering_check",
    ],
)

public_headers_no_gen = glob([
    "include/gz/common/*.hh",
])

sources = glob(
    ["src/*.cc"],
    exclude = ["src/*_TEST.cc"],
)

test_sources = glob(["src/*_TEST.cc"])

gz_export_header(
    name = "include/gz/common/io/Export.hh",
    export_base = "GZ_COMMON_IO",
    lib_name = "gz-common-io",
    visibility = ["//visibility:private"],
)

generate_include_header(
    name = "iohh_genrule",
    out = "include/gz/common/io.hh",
    hdrs = public_headers_no_gen + [
        "include/gz/common/io/Export.hh",
    ],
)

public_headers = public_headers_no_gen + [
    "include/gz/common/io/Export.hh",
    "include/gz/common/io.hh",
]

cc_library(
    name = "io",
    srcs = sources,
    hdrs = public_headers,
    includes = ["include"],
    deps = [
        GZ_ROOT + "gz_common",
        GZ_ROOT + "gz_math",
    ],
)

cc_binary(
    name = "libgz-common5-io.so",
    includes = ["include"],
    linkopts = ["-Wl,-soname,libgz-common5-io.so"],
    linkshared = True,
    deps = [":events"],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src],
    deps = [
        ":io",
        GZ_ROOT + "gz_common/test:test_utils",
        "@gtest",
        "@gtest//:gtest_main",
    ],
) for src in test_sources]
